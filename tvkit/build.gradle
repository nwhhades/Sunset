plugins {
    id 'com.android.library'
    id 'maven-publish'
}

def groupIdStr = "com.whiner.tvkit"
def artifactIdStr = "tv_kit"
def versionStr = "1.0"

android {
    namespace 'com.whiner.tvkit'
    compileSdk 33

    defaultConfig {
        minSdk 19
        targetSdk 33

        aarMetadata {
            minCompileSdk = 19
        }

        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'consumer-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
    }
    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

dependencies {

    api 'androidx.appcompat:appcompat:1.6.1'
    api 'com.google.android.material:material:1.8.0'
    api 'androidx.constraintlayout:constraintlayout:2.1.4'

    //工具箱-不需要混淆
    api 'com.blankj:utilcodex:1.31.1'
    //图片库-不需要混淆
    api 'com.github.bumptech.glide:glide:4.14.2'
    //吐司框架-不需要混淆 https://github.com/getActivity/Toaster
    api 'com.github.getActivity:Toaster:12.2'
    //权限请求框架-不需要混淆 https://github.com/getActivity/XXPermissions
    api 'com.github.getActivity:XXPermissions:18.0'
    //缓存-不需要混淆
    api 'com.tencent:mmkv:1.2.15'
    //GSON-不需要混淆
    api 'com.google.code.gson:gson:2.10.1'

    //okhttp-需要混淆
    //noinspection GradleDependency
    api 'com.squareup.okhttp3:okhttp:3.12.13'
    //noinspection GradleDependency
    api 'com.squareup.okhttp3:logging-interceptor:3.12.13'
    //Retrofit2
    //noinspection GradleDependency
    api 'com.squareup.retrofit2:retrofit:2.6.4'
    //noinspection GradleDependency
    api 'com.squareup.retrofit2:converter-gson:2.6.4'
    //noinspection GradleDependency
    api 'com.squareup.retrofit2:converter-scalars:2.6.4'
    //noinspection GradleDependency
    api 'com.squareup.retrofit2:adapter-rxjava2:2.6.4'
    //RxJava-不需要混淆规则
    api 'io.reactivex.rxjava2:rxandroid:2.1.1'
    api 'io.reactivex.rxjava2:rxjava:2.2.21'
    //事件总线程-需要混淆规则
    api 'org.greenrobot:eventbus:3.3.1'

}

publishing {
    publications {
        release(MavenPublication) {
            groupId = groupIdStr
            artifactId = artifactIdStr
            version = versionStr

            afterEvaluate {
                from components.release
            }
        }
    }
}